buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
	}
}

plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'maven'
	id 'maven-publish'
	id 'jacoco'
	id "org.sonarqube" version "2.7"
	id 'org.unbroken-dome.test-sets' version '2.2.1'
	id 'io.codearte.nexus-staging' version '0.11.0'
}

apply plugin: 'com.bmuschko.nexus'

group = 'com.github.jferrater'
archivesBaseName = 'opa-ast-to-sql-query-java'
version = '0.1.2-SNAPSHOT'

java {
	sourceCompatibility(JavaVersion.VERSION_11)
	targetCompatibility(JavaVersion.VERSION_11)
}

jacoco {
	toolVersion = '0.8.2'
	reportsDir = file("${buildDir}/reports/jacoco")
}

repositories {
	mavenLocal()
	mavenCentral()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

dependencies {
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
	runtimeOnly group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'
	annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.mariadb.jdbc:mariadb-java-client:2.3.0'
	testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.2.10'
	testImplementation group: 'org.testcontainers', name: 'mysql', version: '1.12.5'
	testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.12.5'
	testImplementation group: 'com.h2database', name: 'h2', version: '1.4.197'
	testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

testSets {
	integrationTest
}

test {
	useJUnitPlatform()
}

integrationTest {
	useJUnitPlatform()
	integrationTest.outputs.upToDateWhen { false }
	failFast = true
	dependsOn 'cleanIntegrationTest'

	testLogging {
		events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
	}
}

sonarqube {
	properties {
		property 'sonar.projectName', 'Open Policy Agent AST to SQL Query Spring Boot Library'
		property 'sonar.github.repository', 'jferrater/opa-ast-to-sql-query-java'
		property 'sonar.projectKey', 'jferrater_opa-ast-to-sql-query-java'
		property 'sonar.organization', 'jferrater'
		property 'sonar.host.url', 'https://sonarcloud.io'
		property 'sonar.projectVersion', version
		property 'sonar.login', System.getenv('SONAR_TOKEN')
		property 'sonar.java.coveragePlugin', 'jacoco'
		property 'sonar.jacoco.reportPath', 'build/reports/jacoco/test/jacocoTestReport.xml'
	}
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			groupId = 'com.github.jferrater'
			artifactId = 'opa-ast-to-sql-query-java'
			version = '0.1.2-SNAPSHOT'
			from components.java
		}
	}
}

modifyPom {
	project {
		name 'opa-ast-to-sql-query-java'
		description 'Spring Boot library for translating Open Policy Agent compiler API response to SQL query/statements.'
		url 'https://github.com/jferrater/opa-ast-to-sql-query-java'
		inceptionYear '2020'

		scm {
			url 'https://github.com/jferrater/opa-ast-to-sql-query-java'
			connection 'scm:https://github.com/jferrater/opa-ast-to-sql-query-java.git'
			developerConnection 'scm:https://github.com/jferrater/opa-ast-to-sql-query-java.git'
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id 'jferrater'
				name 'Joffry Ferrater'
				email 'joffry.ferrater@gmail.com'
			}
		}
	}
}

extraArchive {
	sources = true
	tests = true
	javadoc = true
}

nexus {
	sign = true
	repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}